result-view {
  match: VideoInfo (videoInfos) {
    from-output: GetVideo (action)
  }

  message {
    if (size(videoInfos) == 0) {
      template-macro (GetVideo_Result_Zero)
    } else-if (size(videoInfos) == 1) {
      template-macro (GetVideo_Result_One)
    } else {
      if (exists(action.searchTerm)) {
        template-macro (GetVideo_Result_SearchTerm) {
          param (searchTerm) {
            expression (action.searchTerm)
          }
          param (size) {
            expression (size(videoInfos))
          }
        }
      } else-if (exists(action.category)) {
        template-macro (GetVideo_Result_Category) {
          param (category) {
            expression (action.category)
          }
          param (size) {
            expression (size(videoInfos))
          }
        }
      }
    }
  }

  render {
    if (size(videoInfos) > 1) {
      list-of (videoInfos) {
        navigation-mode {
          if ($handsFree) {
            read-many-and-next {
              page-size (5)
              list-summary {
                if (exists(action.searchTerm)) {
                  template-macro (GetVideo_Result_SearchTerm) {
                    param (searchTerm) {
                      expression (action.searchTerm)
                    }
                    param (size) {
                      expression (size(videoInfos))
                    }
                  }
                } else-if (exists(action.category)) {
                  template-macro (GetVideo_Result_Category) {
                    param (category) {
                      expression (action.category)
                    }
                    param (size) {
                      expression (size(videoInfos))
                    }
                  }
                }
              }
              page-content (page){
                underflow-statement {
                  template ("이전 목록이 없네요.")
                }
                overflow-statement {
                  template ("모두 확인했어요.")
                }
                next-page-question {
                  template ("다음 페이지로 넘어갈까요?")
                }
              }
            }
          }
        }
        where-each (videoInfo) {
          spoken-summary {
            template ("#{value(videoInfo)}")
          }
          layout-match (videoInfo) {
            mode (Summary)
          }
        }
      }
    } else-if (size(videoInfos) == 1) {
      layout-match (videoInfos) {
        mode (Details)
      }
    } else {
      nothing
    }
  }
}