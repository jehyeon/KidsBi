result-view {
	match: QuizInfo (quizInfos) {
		from-output: GetQuiz (action)
	}
	
	message {
		if (size(quizInfos) == 0) {
			template-macro (GetQuiz_Result_Zero)
		} else-if (size(quizInfos) == 1) {
			template-macro (GetQuiz_Result_One)
		} else {
			template-macro (GetQuiz_Result_Multi){
				param (size) {
					expression (size(quizInfos))
				}
				param (category) {
					expression (action.category)
				}
			}
		}
	}
	
	render {
		if (size(quizInfos) > 1) {
			list-of (quizInfos) {
				navigation-mode {
					if($handsFree) {
						read-many-and-next {
							page-size (5)
							list-summary {
								if (size(quizInfos) > 1) {
									template-macro (GetQuiz_Result_Multi){
										param (size) {
											expression (size(quizInfos))
										}
										param (category) {
											expression (action.category)
										}
									}
								}
							}
							page-content (page) {
								underflow-statement {template-macro (List_Navigate_Underflow)}
								overflow-statement { template-macro (List_Navigate_Overflow)}
								next-page-question { template-macro (List_Navigate_question)}
							}
						}
					}
				}
				where-each (quizInfo) {
					spoken-summary {
						template ("#{value(quizInfo)}")
					}
					layout-match (quizInfo) {
						mode (Summary)
					}
				}
			}
		} else-if (size(quizInfos) == 1) {
			layout-match (quizInfos) {
				mode (Details)
			}
		} else {
			nothing
		}
	}
	conversation-drivers {
		if (size(quizInfos) > 1){
			conversation-driver {
				template-macro (StartQuiz_This_Card)
			}
		}
	}
}